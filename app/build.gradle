apply plugin: 'com.android.application'
apply plugin: 'org.sonarqube'
apply plugin: 'com.github.triplet.play'

android {
    signingConfigs {
        release {
            storeFile = file("../ignore/macrologKeyStore")
            keyAlias = "secret"
            storePassword = "secret"
            keyPassword = "secret"
        }
    }

    def isRunningOnTravis = System.getenv("CI") == "true"

    if (isRunningOnTravis) {
        signingConfigs.release.storeFile = file("../macrologKeyStore")
        signingConfigs.release.keyAlias = System.getenv("keystore_alias")
        signingConfigs.release.storePassword = System.getenv("keystore_store_password")
        signingConfigs.release.keyPassword = System.getenv("keystore_password")
    }

    compileSdkVersion 28
    defaultConfig {
        applicationId "com.csl.macrologandroid"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 8
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.debug
    }
    buildTypes {
        debug {
            debuggable true
            buildConfigField 'String', 'SERVER_URL', '"https://macrolog-backend.herokuapp.com/"'
        }
        development {
            applicationIdSuffix ".dev"
            debuggable true
            buildConfigField 'String', 'SERVER_URL', '"http://192.168.178.35:8090/"'
        }
        acceptance {
            applicationIdSuffix ".acc"
            debuggable true
            buildConfigField 'String', 'SERVER_URL', '"http://macrolog-backend-acc.herokuapp.com"'
        }
        release {
            minifyEnabled false
            buildConfigField 'String', 'SERVER_URL', '"https://macrolog-backend.herokuapp.com/"'
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/license.txt'
        exclude 'META-INF/notice.txt'
    }
}

play {
    serviceAccountCredentials = file("../macrolog-service-account-key.json")
    track = "beta"
    defaultToAppBundles = true
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.core:core:1.0.2'
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.13'
    implementation 'org.jetbrains:annotations:15.0'
    implementation 'org.springframework.android:spring-android-rest-template:1.0.1.RELEASE'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
