plugins {
    id "com.android.application"
    id "com.github.triplet.play"
    id "org.sonarqube"
}

android {
    compileSdkVersion 29
    buildToolsVersion = '29.0.2'

    defaultConfig {
        applicationId "com.csl.macrologandroid"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 16
        versionName "1.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.debug
    }

    signingConfigs {
        release {
            storeFile = null
            keyAlias = "secret"
            storePassword = "secret"
            keyPassword = "secret"
        }
    }

    def isRunningOnTravis = System.getenv("CI") == "true"
    if (isRunningOnTravis) {
        signingConfigs.release.storeFile = file("../macrologKeyStore")
        signingConfigs.release.keyAlias = System.getenv("keystore_alias")
        signingConfigs.release.storePassword = System.getenv("keystore_store_password")
        signingConfigs.release.keyPassword = System.getenv("keystore_password")
    }
    def isRunningLocal = System.getenv("CI") == "false"
    if (isRunningLocal) {
        signingConfigs.release.storeFile = file("../ignore/macrologKeyStore")
        signingConfigs.release.keyAlias = file("../ignore/keystore.properties").getText().getProperties().get('keyAlias')
        signingConfigs.release.storePassword = file("../ignore/keystore.properties").getText().getProperties().get('storePassword')
        signingConfigs.release.keyPassword = file("../ignore/keystore.properties").getText().getProperties().get('keyPassword')
    }

    buildTypes {
        debug {
            debuggable true
            buildConfigField 'String', 'SERVER_URL', '"https://macrolog-backend.herokuapp.com/"'
        }
        development {
            applicationIdSuffix ".dev"
            debuggable true
            buildConfigField 'String', 'SERVER_URL', '"http://192.168.178.23:8090/"'
        }
        acceptance {
            applicationIdSuffix ".acc"
            debuggable true
            buildConfigField 'String', 'SERVER_URL', '"http://macrolog-backend-acc.herokuapp.com"'
        }
        release {
            shrinkResources true
            minifyEnabled true
            buildConfigField 'String', 'SERVER_URL', '"https://macrolog-backend.herokuapp.com/"'
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/license.txt'
        exclude 'META-INF/notice.txt'
    }
}

play {
    promoteTrack.set("beta")
    serviceAccountCredentials.set(file("../ignore/macrolog-service-account-key.json"))
    defaultToAppBundles.set(true)
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.core:core:1.3.2'
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.13'
    implementation 'org.jetbrains:annotations:15.0'
    implementation 'org.springframework.android:spring-android-rest-template:1.0.1.RELEASE'
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}
